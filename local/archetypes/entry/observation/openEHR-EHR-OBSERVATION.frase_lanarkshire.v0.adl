archetype (adl_version=1.4; uid=f29e2a32-a9a5-478f-850b-5476633547b1)
	openEHR-EHR-OBSERVATION.frase_lanarkshire.v0

concept
	[at0000]	-- Falls Risk Assessment Scale (NHS Lanarkshire)
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heidi Koikkalainen">
		["organisation"] = <"freshEHR Clinical Informatics, UK">
		["email"] = <"heidi@freshehr.com">
		["date"] = <"2021-05-10">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a falls risk assessment using the NHS Lanarkshire Falls Risk Assessment Scale for Elderly tool.">
			use = <"Use to record a falls risk assessment using the NHS Lanarkshire Falls Risk Assessment Scale for Elderly tool.">
			keywords = <"risk, fall, balance, gait, mobility", ...>
			misuse = <"">
			copyright = <"© Apperta Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Ian McNicoll, freshEHR Clinical Informatics, UK", ...>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"Apperta UK">
		["references"] = <"[Internet]. South Lanarkshire Council. 2021 [cited 2021 May 10]. Available from: https://www.southlanarkshire.gov.uk/downloads/file/5946/falls_prevention_in_lanarkshire_care_homes .">
		["current_contact"] = <"Heidi Koikkalainen, heidi@freshehr.com">
		["original_namespace"] = <"uk.org.clinicalmodels">
		["original_publisher"] = <"Apperta UK">
		["custodian_namespace"] = <"uk.org.clinicalmodels">
		["MD5-CAM-1.0.1"] = <"23073EEFA30303EF46562586300C8FD9">
		["build_uid"] = <"fbcad56a-58b6-446c-8262-4d71ec7f322a">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Falls Risk Assessment Scale (NHS Lanarkshire)
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..*} matches {    -- Any point in time event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0006] occurrences matches {0..1} matches {    -- Sex
										value matches {
											1|[local::at0007],
											2|[local::at0008]
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {    -- Age
										value matches {
											1|[local::at0010],
											2|[local::at0011],
											1|[local::at0012]
										}
									}
									ELEMENT[at0013] occurrences matches {0..*} matches {    -- Gait
										value matches {
											0|[local::at0014],
											1|[local::at0015],
											3|[local::at0016],
											3|[local::at0017]
										}
									}
									ELEMENT[at0018] occurrences matches {0..*} matches {    -- Sensory deficit
										value matches {
											2|[local::at0019],
											1|[local::at0020],
											2|[local::at0021]
										}
									}
									ELEMENT[at0022] occurrences matches {0..1} matches {    -- Fall history
										value matches {
											0|[local::at0023],
											2|[local::at0024],
											1|[local::at0025],
											3|[local::at0026]
										}
									}
									ELEMENT[at0027] occurrences matches {0..*} matches {    -- Medication
										value matches {
											1|[local::at0028],
											1|[local::at0029],
											1|[local::at0030]
										}
									}
									ELEMENT[at0044] occurrences matches {0..*} matches {    -- Medical history
										value matches {
											1|[local::at0045],
											1|[local::at0046],
											1|[local::at0047],
											1|[local::at0048],
											1|[local::at0049]
										}
									}
									ELEMENT[at0034] occurrences matches {0..1} matches {    -- Mobility
										value matches {
											0|[local::at0035],
											2|[local::at0036],
											3|[local::at0037],
											1|[local::at0038]
										}
									}
									ELEMENT[at0039] occurrences matches {0..1} matches {    -- Total score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..30|}
											}
										}
									}
									ELEMENT[at0040] occurrences matches {0..1} matches {    -- Interpretation
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0041,    -- Low risk
													at0042,    -- Medium risk
													at0043]    -- High risk
												}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0004] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Falls Risk Assessment Scale (NHS Lanarkshire)">
					description = <"A falls risk assessment tool used in NHS Lanarkshire, based on Cannard's Falls Risk Assessment Scale for the Elderly (FRASE) tool.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any point in time event">
					description = <"">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0005"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0006"] = <
					text = <"Sex">
					description = <"The sex of the individual.">
				>
				["at0007"] = <
					text = <"Male">
					description = <"The individual is male.">
				>
				["at0008"] = <
					text = <"Female">
					description = <"The individual is female.">
				>
				["at0009"] = <
					text = <"Age">
					description = <"The age range of the individual.">
				>
				["at0010"] = <
					text = <"60-70">
					description = <"The individual is aged between 60-70.">
				>
				["at0011"] = <
					text = <"71-80">
					description = <"The individual is aged between 71-80.">
				>
				["at0012"] = <
					text = <"81+">
					description = <"The individual is aged 81 or over.">
				>
				["at0013"] = <
					text = <"Gait">
					description = <"The gait of the individual.">
				>
				["at0014"] = <
					text = <"Steady">
					description = <"The individual's gait is steady.">
				>
				["at0015"] = <
					text = <"Hesitant">
					description = <"The individual is hesitant in initiating movement.">
				>
				["at0016"] = <
					text = <"Poor transfer">
					description = <"The individual requires help with transfers, i.e. getting in or out of bed, getting on or off a chair, and moving from chair to standing.">
				>
				["at0017"] = <
					text = <"Unsteady">
					description = <"The individual's gait is unsteady.">
				>
				["at0018"] = <
					text = <"Sensory deficit">
					description = <"The sensory deficit(s) of the individual.">
				>
				["at0019"] = <
					text = <"Sight">
					description = <"The individual has a sight deficit.">
				>
				["at0020"] = <
					text = <"Hearing">
					description = <"The individual has a hearing deficit.">
				>
				["at0021"] = <
					text = <"Balance">
					description = <"The individual has a balance deficit.">
				>
				["at0022"] = <
					text = <"Fall history">
					description = <"The individual's history of falls in the last 12 months.">
				>
				["at0023"] = <
					text = <"None">
					description = <"The individual has not had any falls in the last 12 months.">
				>
				["at0024"] = <
					text = <"At home prior to admission">
					description = <"The individual has had a fall at home prior to admission to the care home in the last 12 months.">
				>
				["at0025"] = <
					text = <"Within care home">
					description = <"The individual has had a fall within the care home in the last 12 months.">
				>
				["at0026"] = <
					text = <"Both">
					description = <"The individual has had a fall both at home and within the care home in the last 12 months.">
				>
				["at0027"] = <
					text = <"Medication">
					description = <"Medication associated with falls risk.">
				>
				["at0028"] = <
					text = <"Hypnotics (sleeping tablets)">
					description = <"The individual takes hypnotics.">
				>
				["at0029"] = <
					text = <"Tranquilisers (sedatives)">
					description = <"The individual takes tranquilisers.">
				>
				["at0030"] = <
					text = <"Hypertensive (blood pressure)">
					description = <"The individual takes antihypertensive medication.">
				>
				["at0034"] = <
					text = <"Mobility">
					description = <"The level of mobility of the individual.">
				>
				["at0035"] = <
					text = <"Fully mobile">
					description = <"The individual is fully mobile.">
				>
				["at0036"] = <
					text = <"Uses aid">
					description = <"The individual uses a mobility aid.">
				>
				["at0037"] = <
					text = <"Restricted">
					description = <"The individual's mobility is restricted.">
				>
				["at0038"] = <
					text = <"Bed bound">
					description = <"The individual is bed bound.">
				>
				["at0039"] = <
					text = <"Total score">
					description = <"The overall FRASE score.">
				>
				["at0040"] = <
					text = <"Interpretation">
					description = <"Interpretation of the overall FRASE score.">
				>
				["at0041"] = <
					text = <"Low risk">
					description = <"Overall score 3-8; low falls risk.">
				>
				["at0042"] = <
					text = <"Medium risk">
					description = <"Overall score 9-12; medium falls risk.">
				>
				["at0043"] = <
					text = <"High risk">
					description = <"Overall score 13+; high falls risk.">
				>
				["at0044"] = <
					text = <"Medical history">
					description = <"Medical conditions associated with falls risk.">
				>
				["at0045"] = <
					text = <"Diabetes">
					description = <"The individual has diabetes.">
				>
				["at0046"] = <
					text = <"Organic brain disease/confusion">
					description = <"The individual has an organic brain disease/episodes of confusion.">
				>
				["at0047"] = <
					text = <"Fits">
					description = <"The individual has fits.">
				>
				["at0048"] = <
					text = <"Incontinent">
					description = <"The individual is incontinent.">
				>
				["at0049"] = <
					text = <"Inability to co-operate">
					description = <"The individual is unable to co-operate.">
				>
			>
		>
	>
